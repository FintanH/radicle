#!/usr/bin/env radicle

(load! (find-module-file! "prelude.rad"))
(load! (find-module-file! "monadic/registry.rad"))
(file-module! "prelude/io-utils.rad")
(file-module! "prelude/error-messages.rad")
(file-module! "monadic/items.rad")

(import prelude/error-messages :as 'error)
(import monadic/items :as 'items)
(import prelude/strings '[split-by] :unqualified)
(import prelude/validation :as 'validation)
(import prelude/machine :as 'machine)
(import prelude/io :as 'io)
(import prelude/io-utils :as 'io)

(def path!
  (fn []
    (string-append (io/base-path!) "/registry.rad")))

(def init-registry-file!
  (fn []
    (io/shell! (string-append "mkdir -p " (io/base-path!)) "")
    (io/init-file-dict! (path!))))

;; TODO Replace with server hosted machine
(def default-machine-name "12D3KooWNdyJ1QpNy5RbBfjKEx1gPvRJ72tgYo1vs6jvXU8yuQN2")

(def machine-name
  (fn []
    (init-registry-file!)
    (match (io/read-file-key! (path!) :registry)
      :nothing    default-machine-name
      [:just 'r]  r)))

(def help
  (string-append
  "rad registry - Radicle Registry CLI

   Usage:
        rad registry list [--unread]
        rad registry new
        rad registry [show | delete | mark-read | mark-unread] <project-number>
        rad registry all-read
        rad registry init
        rad registry set <registry-id>
        rad registry help

     list         - Lists all projects
                    If the option '--unread' is appended, only unread projects
                    are listed.
                    Unread projects are marked with a * after the timestamp.
     new          - Register a new project in $EDITOR
     show         - Show a project
     show-id      - Show the project id
     delete       - Delete a project
                    This command is restricted to the maintainer of the registry
                    and the registrant.
     mark-read    - Mark project as read
     mark-unread  - Mark project as unread
     all-read     - Mark all projects as read
     init         - Initialize a new registry machine
     set          - Set the registry that is used for the commands instead of
                    the default. Delete the file to use the default registry.
     help         - Print this help and exit
       "))

(def cmd-parse-failure
  (fn [error]
    (parse-failure error help)))

(def get-project
  "Returns the project of given number `n`."
  (fn [n machine]
    (items/verify-item-number n (list-projects machine) :project)))

(def show-project!
  "Shows a single PROJECT `n`"
  (fn [n machine]
    (def project (get-project n machine))
    (put-str! (items/pretty-item-view (items/enrich-item machine project)))
    (newness/mark-read! machine n)))

(def show-project-id!
  "Shows the id of a single PROJECT `n`"
  (fn [n machine]
    (def project (get-project n machine))
    (put-str! (lookup :project-id project))
    (newness/mark-read! machine n)))

(def delete-project
  (fn [n machine]
    (catch 'daemon-error
      (do
        (get-project n machine)
        (delete-project! machine {:project-number n})
        (put-str! (string-append "Project #" (show n) " has been deleted."))
        (newness/mark-read! machine n))
      (fn [_]
        (put-str! (error/state-change-failure :project "deleted"))
        (exit! 1)))))

(def list
  (fn [machine options]
    (def projects (values (list-projects machine)))
    (items/list-items-plain machine options projects :project)))

(def prompt-for-metadata!
  (fn []
    (def name (prompt-non-empty!
                "? What's the name of your project: "
                \(not (eq? ? ""))
                "Project name can't be empty!"))
    (def desc (prompt! "? Briefly describe the project: "))
    (def id (prompt-non-empty!
              "? What's the id of your project: "
              \(not (eq? ? ""))
              "Project id can't be empty!"))
    (def labels (prompt! "? Do you want to add any labels (comma seperated): "))
    {:name name :description desc :project-id id :labels (split-by (fn [x] (eq? x ",")) labels)}))

(def new-project
  (fn [machine]
    (def meta (prompt-for-metadata!))
    (def author {:git-username (get-git-username!)})
    (match (register-project!  machine (simple-project (<> meta author)))
           ['n] (do (put-str! (string-append "Created project #" (show n) " in " machine))
                    (newness/mark-read! machine n))
           _    (put-str! (error/no-number-returned :project)))))

(def cmd-options
  [
    { :key :unread :type :flag :options ["--unread"] :default #f }
  ])

(def /list-cmd
  (fn [opts]
    (/cmd-opts "list" opts cmd-options help)))

(def args (get-args!))

(def whole-project-num
  (fn [action num-str f]
    (whole-num help :project action num-str f)))

(def mark-all-read!
  (fn [machine]
    (map (fn [i] (newness/mark-read! machine (lookup :number i)))
         (values (list-projects machine)))))

(def new-registry!
  (fn []
    (def registry (create-registry-machine!))
    (put-str! "=> Assembled rad registry machine")
    (put-str! (string-append "=> registry id: " registry))))

(def set-registry!
  (fn [id]
    (init-registry-file!)
    (io/write-file-key! (path!) :registry id)
    (put-str! (string-append "=> Set registry machine to " id))))

(machine/catch-daemon!
 (fn []
   (match args
          (/list-cmd 'options)           (list (machine-name) options)
          (/cmd-0 "new" help)            (new-project (machine-name))
          (/cmd-0 "init" help)           (new-registry!)
          (/cmd-1 "set" 'n help)         (set-registry! n)
          (/cmd-1 "delete" 'n help)      (whole-project-num "delete" n (fn [n] (delete-project n (machine-name))))
          (/cmd-1 "show" 'n help)        (whole-project-num "show" n (fn [n] (show-project! n (machine-name))))
          (/cmd-1 "show-id" 'n help)     (whole-project-num "show-id" n (fn [n] (show-project-id! n (machine-name))))
          (/cmd-1 "mark-read" 'n help)   (whole-project-num "mark as read" n (fn [n] (newness/mark-read! (machine-name) n)))
          (/cmd-1 "mark-unread" 'n help) (whole-project-num "mark as unread" n (fn [n] (newness/mark-unread! (machine-name) n)))
          (/cmd-0 "all-read" help)       (mark-all-read! (machine-name))
          (/cmd-help)                    (put-str! help)
          (/cons 'cmd _)                 (cmd-parse-failure (error/unknown-command cmd))
          []                             (put-str! help))))
